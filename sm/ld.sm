# $Id: ld.sm,v 8.1 2015/04/20 11:14:17 hmb Exp $

clcd	12	# Plot Cd & Cl timeseries from .sta file

		# _mode == 0 ==> no axis labels
		# _mode == 1 ==> abscissa labels
		# _mode == 2 ==> ordinate labels
		# _mode == 3 ==> both

		IF( $?2 ) { SET _mode = $2 } ELSE { SET _mode = 0 }
	
		DATA $1
		READ { t_his 2 drag 8 lift 14 }

		SET npts = DIMEN(t_his)
		SET tmax = t_his[npts - 1]
		SET time = t_his - tmax + 50.
		SET cd   = 2 * drag
		SET cl   = 2 * lift

		ERASE
		LWEIGHT 2
		EXPAND 1.3
		LOCATION 4000 31000 4000 13270
		WINDOW 1 1 1 1
		LIMITS 0 50 -4 4
		
		IF ( _mode == 0 ) { BOX 0 0 }
		IF ( _mode == 1 ) {
			BOX 1 0
			XLABEL {\it tU / D}
		}
		IF ( _mode == 2 ) {
			BOX 0 2
			YLABEL {{\it C_d} , {\it C_l}}
		 }
		IF ( _mode == 3 ) {
			BOX 1 2
			XLABEL {\it tU / D}
			YLABEL {{\it C_d} , {\it C_l}}
		}

		LTYPE 0
		CONNECT time cd
		LTYPE 3
		CONNECT time cl
		
		LTYPE 1
		RELOCATE 0 0
		DRAW 50 0

